check_include_file(math.h HAS_MATH_H)
check_include_file(malloc.h HAS_MALLOC_H)
if(NOT HAS_MALLOC_H)
    set(HAS_MALLOC_H 0)
endif()
check_include_file(malloc/malloc.h HAS_MALLOC_MALLOC_H)
if(NOT HAS_MALLOC_MALLOC_H)
    set(HAS_MALLOC_MALLOC_H 0)
endif()

check_include_file(sched.h HAS_SCHED_H)
check_include_file(sys/mman.h HAS_MMAN_H)
check_include_file(string.h HAS_STRING_H)
check_include_file(time.h HAS_TIME_H)

set(X264_STACK_ALIGNMENT 4)
if(CMAKE_SIZEOF_VOID_P EQUAL 8)
    set(X264_STACK_ALIGNMENT 16)
endif()
set(_saved_required_flags "${CMAKE_REQUIRED_FLAGS}")
set(X264_STACK_ALIGNMENT_FLAG "")
check_c_compiler_flag(-mpreferred-stack-boundary=6 HAS_PREF_STACK_BOUNDARY6)
check_c_compiler_flag(-mstack-alignment=64 HAS_MSTACK_ALIGNMENT64)
check_c_compiler_flag(-mpreferred-stack-boundary=4 HAS_PREF_STACK_BOUNDARY4)
check_c_compiler_flag(-mstack-alignment=16 HAS_MSTACK_ALIGNMENT16)
if(HAS_PREF_STACK_BOUNDARY6)
    set(CMAKE_REQUIRED_FLAGS "-mpreferred-stack-boundary=6")
    check_C_source_compiles("void main(){}" BUILDSWITH_PREF_STACK_BOUNDARY6)
    if(BUILDSWITH_PREF_STACK_BOUNDARY6)
        set(X264_STACK_ALIGNMENT 64)
        set(X264_STACK_ALIGNMENT_FLAG "-mpreferred-stack-boundary=6")
    endif()
elseif(HAS_MSTACK_ALIGNMENT64)
    set(CMAKE_REQUIRED_FLAGS "-mstack-alignment=64")
    check_C_source_compiles("void main(){}" BUILDSWITH_MSTACK_ALIGN64)
    if(BUILDSWITH_MSTACK_ALIGN64)
        set(X264_STACK_ALIGNMENT 64)
        set(X264_STACK_ALIGNMENT_FLAG "-mstack-alignment=64")
    endif()
endif()
if(${X264_STACK_ALIGNMENT} LESS 16)
    if(HAS_PREF_STACK_BOUNDARY4)
        set(CMAKE_REQUIRED_FLAGS "-mpreferred-stack-boundary=4")
        check_C_source_compiles("void main(){}" BUILDSWITH_PREF_STACK_BOUNDARY4)
        if(BUILDSWITH_PREF_STACK_BOUNDARY4)
            set(X264_STACK_ALIGNMENT 16)
            set(X264_STACK_ALIGNMENT_FLAG "-mpreferred-stack-boundary=4")
        endif()
    elseif(HAS_MSTACK_ALIGNMENT16)
        set(CMAKE_REQUIRED_FLAGS "-mstack-alignment=16")
        check_C_source_compiles("void main(){}" BUILDSWITH_MSTACK_ALIGN16)
        if(BUILDSWITH_MSTACK_ALIGN16)
            set(X264_STACK_ALIGNMENT 16)
            set(X264_STACK_ALIGNMENT_FLAG "-mstack-alignment=16")
        endif()
    endif()
endif()
set(CMAKE_REQUIRED_FLAGS "${_saved_required_flags}")

if(X264_STACK_ALIGNMENT_FLAG AND (ARCH_X86 OR ARCH_X86_64) AND (GCC OR CLANG))
    x264_append_compile_options(${X264_STACK_ALIGNMENT_FLAG})
endif()

if(HAS_MATH_H)
    if(NOT MSVC)
        set(CMAKE_REQUIRED_LIBRARIES -lm)
    endif()
    check_symbol_exists(log2f math.h HAS_LOG2F)
else()
    set(HAS_LOG2F 0)
endif()

if(HAS_STRING_H)
    check_symbol_exists(strtok_r string.h HAS_STRTOK_R)
else()
    set(HAS_STRTOK_R 0)
endif()

if(HAS_TIME_H)
    check_symbol_exists(clock_gettime time.h HAS_CLOCK_GETTIME)
else()
    set(HAS_CLOCK_GETTIME 0)
endif()

if(HAS_MMAN_H)
    check_symbol_exists(MAP_PRIVATE sys/mman.h HAS_MMAP)
    if(NOT HAS_MMAP)
        set(HAS_MMAP 0)
    endif()
    check_symbol_exists(MADV_HUGEPAGE sys/mman.h HAS_THP)
    if(NOT HAS_THP)
        set(HAS_THP 0)
    endif()
else()
    set(HAS_THP 0)
    set(HAS_MMAP 0)
endif()

check_c_source_compiles("#include <stdint.h>\nint main(void){uint32_t test_vec __attribute__ ((vector_size (16))) = {0,1,2,3}; return 0;}" HAS_VECTOREXT)
if(NOT HAS_VECTOREXT)
    set(HAS_VECTOREXT 0)
endif()

# Large file support detection
set(CMAKE_REQUIRED_DEFINITIONS "-D_LARGEFILE_SOURCE -D_FILE_OFFSET_BITS=64")
check_c_source_compiles("#include <stdio.h>\nint main(void) { fseeko(stdin, 0, 0); return 0; }" HAS_FSEEKO)
check_c_source_compiles("#include <stdio.h>\nint main(void) { fseeko64(stdin, 0, 0); return 0; }" HAS_FSEEKO64)
check_c_source_compiles("#include <stdio.h>\nint main(void) { _fseeki64(stdin, 0, 0); return 0; }" HAS_FSEEKI64)
set(CMAKE_REQUIRED_DEFINITIONS "")

if(HAS_FSEEKO)
    set(X264_FSEEK fseeko)
    set(X264_FTELL ftello)
elseif(HAS_FSEEKO64)
    set(X264_FSEEK fseeko64)
    set(X264_FTELL ftello64)
elseif(HAS_FSEEKI64)
    set(X264_FSEEK _fseeki64)
    set(X264_FTELL _ftelli64)
else()
    set(X264_FSEEK fseek)
    set(X264_FTELL ftell)
endif()

set(X264_USE_GPL 0)
if(ENABLE_GPL)
    set(X264_USE_GPL 1)
endif()

if(X86 OR X86_64)
    check_c_source_compiles("int main(void){__asm__(\"pabsw %xmm0, %xmm0\"); return 0;}" HAS_X86_INLINE_ASM)
    if(HAS_X86_INLINE_ASM)
        check_c_source_compiles("int main(void){__asm__(\"pabsw %xmm0, %xmm0\"); return 0;}" HAS_MMX)
    else()
        set(HAS_X86_INLINE_ASM 0)
        set(HAS_MMX 0)
    endif()

    if(MSVC AND NOT HAS_X86_INLINE_ASM)
        check_c_source_compiles("int main(void){__asm pabsw xmm0, xmm0; return 0;}" HAS_X86_INLINE_ASM)
        if(HAS_X86_INLINE_ASM)
            check_c_source_compiles("int main(void){__asm pabsw xmm0, xmm0; return 0;}" HAS_MMX)
        else()
            set(HAS_X86_INLINE_ASM 0)
            set(HAS_MMX 0)
        endif()
        if(NOT HAS_X86_INLINE_ASM)
            set(CMAKE_REQUIRED_FLAGS "/Oi")
            check_c_source_compiles("#include <emmintrin.h>\nint main(void){int v = 0;__m128i vreg = {0};vreg = _mm_set1_epi32(v);return v;}" HAS_MMX_FROM_INTRINSICS)
            if(NOT HAS_MMX_FROM_INTRINSICS)
                set(HAS_MMX 0)
            else()
                set(HAS_MMX 1)
            endif()
        endif()
    endif()
else()
    set(HAS_X86_INLINE_ASM 0)
    set(HAS_MMX 0)
endif()

if(ARM OR ARM64)
    if(ARM64)
        set(HAS_AARCH64 1)
        set(HAS_NEON 1)
        check_c_source_compiles("int main(void){__asm__(\"cmeq v0.8h, v0.8h, #0\"); return 0;}" HAS_ARM_INLINE_ASM)
    else()
        set(HAS_AARCH64 0)
        set(CMAKE_REQUIRED_FLAGS "-mcpu=cortex-a8 -mfpu=neon")
        check_c_source_compiles("int main(void){__asm__(\"add r0, r1, r2\"); return 0;}" HAS_ARM_INLINE_ASM)
        check_c_source_compiles("int main(void){__asm__(\"rev ip, ip\"); return 0;}" HAS_ARMV6)
        check_c_source_compiles("int main(void){__asm__(\"movt r0, #0\"); return 0;}" HAS_ARMV6T2)
        check_c_source_compiles("int main(void){__asm__(\"vadd.i16 q0, q0, q0\"); return 0;}" HAS_NEON)
        if(NOT HAS_ARMV6)
            set(HAS_ARMV6 0)
        endif()
        if(NOT HAS_ARMV6T2)
            set(HAS_ARMV6T2 0)
        endif()
        if(NOT HAS_NEON)
            set(HAS_NEON 0)
        endif()
    endif()
    if(NOT HAS_ARM_INLINE_ASM)
        set(HAS_ARM_INLINE_ASM 0)
    endif()
else()
    set(HAS_AARCH64 0)
    set(HAS_ARMV6 0)
    set(HAS_ARMV6T2 0)
    set(HAS_NEON 0)
    set(HAS_ARM_INLINE_ASM 0)
endif()

if(ENABLE_THREAD)
    if(WIN32 AND NOT ENABLE_WIN32THREAD)
        set(THREADS_PREFER_PTHREAD_FLAG ON)
    else()
        set(THREADS_PREFER_PTHREAD_FLAG OFF)
    endif()
    find_package(Threads REQUIRED)
    set(HAVE_THREAD 0)
    set(HAVE_POSIXTHREAD 0)
    set(HAVE_WIN32THREAD 0)
    if(THREADS_FOUND)
        set(_has_posix ${CMAKE_USE_PTHREADS_INIT})
        set(_has_win32 ${CMAKE_USE_WIN32_THREADS_INIT})
        if(NOT ENABLE_WIN32THREAD)
            set(_has_win32 0)
        endif()
        if(_has_posix)
            message(STATUS "threading library found: pthreads")
        endif()
        if(_has_win32)
            message(STATUS "threading library found: win32threads")
        endif()
        if(_has_posix AND _has_win32)
            message(STATUS "win32threads and pthreads found!")
            if(WIN32)
                message(STATUS "detected building for Windows, unsetting pthreads")
                set(_has_posix 0)
            elseif(NOT THREADS_PREFER_PTHREAD_FLAG)
                message(STATUS "detected pthreads is not preferred, unsetting pthreads")
                set(_has_posix 0)
            endif()
        endif()
        if(WIN32 AND NOT ENABLE_WIN32THREAD AND NOT _has_posix)
            message(WARNING "win32threads disabled but pthreads not available; threading support will be disabled")
        elseif(_has_posix OR _has_win32)
            set(HAVE_THREAD 1)
            set(HAVE_POSIXTHREAD ${_has_posix})
            set(HAVE_WIN32THREAD ${_has_win32})
        endif()
    else()
        message(STATUS "threading library NOT found")
    endif()
else()
    set(HAVE_POSIXTHREAD 0)
    set(HAVE_WIN32THREAD 0)
    set(HAVE_THREAD 0)
endif()
if(NOT HAVE_THREAD)
    set(HAVE_POSIXTHREAD 0)
    set(HAVE_WIN32THREAD 0)
endif()

set(X264_USE_INTERLACED 0)
if(ENABLE_INTERLACED)
    set(X264_USE_INTERLACED 1)
endif()

if(SYS_LINUX AND HAS_SCHED_H)
    set(CMAKE_REQUIRED_FLAGS -D_GNU_SOURCE)
    check_symbol_exists(CPU_COUNT sched.h HAS_CPU_COUNT)
else()
    set(HAS_CPU_COUNT 0)
endif()

set(X264_USE_INTERLACED 0)
if(ENABLE_INTERLACED)
    set(X264_USE_INTERLACED 1)
endif()

# CMake build system does not support PowerPC/MIPS/LoongArch
# Only x86/x86_64/ARM/ARM64 are supported
set(HAS_VSX 0)
set(HAVE_ALTIVEC 0)
set(HAVE_ALTIVEC_H 0)
set(HAVE_MSA 0)
set(HAVE_LSX 0)
set(HAVE_INTEL_DISPATCHER 0)

if(ARM OR ARM64)
    set(CMAKE_REQUIRED_FLAGS "-Werror")
    check_c_source_compiles("
int main(void) {
    __asm__(\".func test\\n.endfunc\");
    return 0;
}
" HAVE_AS_FUNC)
    set(CMAKE_REQUIRED_FLAGS "")
    if(NOT HAVE_AS_FUNC)
        set(HAVE_AS_FUNC 0)
    endif()
else()
    set(HAVE_AS_FUNC 0)
endif()

if(WIN32 AND MSVC)
    set(CHECK_CODE "
    #include<winapifamily.h>
    #if !WINAPI_FAMILY_PARTITION(WINAPI_PARTITION_DESKTOP)
    int main() { return 0; }
    #else
    #error \"error\"
    #endif
    ")
    check_c_source_compiles(${CHECK_CODE} HAS_WINRT)
else()
    set(HAS_WINRT 0)
endif()

if(ARM64)
    check_c_source_compiles("int main(void){__asm__(\".arch armv8.2-a+sve  \\n ptrue p0.b, vl16\"); return 0;}" HAS_SVE)
    check_c_source_compiles("int main(void){__asm__(\".arch armv8.2-a+sve2 \\n smlalb z10.s, z2.h, z1.h\"); return 0;}" HAS_SVE2)
    if(NOT HAS_SVE)
        set(HAS_SVE 0)
    endif()
    if(NOT HAS_SVE2)
        set(HAS_SVE2 0)
    endif()
else()
    set(HAS_SVE 0)
    set(HAS_SVE2 0)
endif()

# Bit depth support configuration
if(X264_BIT_DEPTH STREQUAL "all")
    message(STATUS "Dual bit-depth mode (8+10)")
    set(HAVE_BITDEPTH8 1)
    set(HAVE_BITDEPTH10 1)
    set(X264_CONFIG_BIT_DEPTH 0)
elseif(X264_BIT_DEPTH STREQUAL "8")
    set(HAVE_BITDEPTH8 1)
    set(HAVE_BITDEPTH10 0)
    message(STATUS "Building for 8-bit depth")
    set(X264_CONFIG_BIT_DEPTH 8)
elseif(X264_BIT_DEPTH STREQUAL "10")
    set(HAVE_BITDEPTH8 0)
    set(HAVE_BITDEPTH10 1)
    message(STATUS "Building for 10-bit depth")
    # OpenCL only supports 8-bit
    set(ENABLE_OPENCL OFF)
    set(X264_CONFIG_BIT_DEPTH 10)
endif()

if(NOT DEFINED X264_CONFIG_BIT_DEPTH)
    set(X264_CONFIG_BIT_DEPTH 0)
endif()

# Chroma format configuration
if(NOT X264_CHROMA_FORMAT STREQUAL "all")
    set(X264_CHROMA_FORMAT_DEFINED 1)
endif()

if(ENABLE_OPENCL)
    if(HAVE_BITDEPTH8 AND HAVE_BITDEPTH10)
        set(HAS_OPENCL "(BIT_DEPTH==8)")
    elseif(HAVE_BITDEPTH8)
        set(HAS_OPENCL 1)
    else()
        set(HAS_OPENCL 0)
    endif()
else()
    set(HAS_OPENCL 0)
endif()

set(HAVE_OPENCL ${HAS_OPENCL})

set(X264_REV 0)
set(X264_REV_DIFF 0)
set(X264_VERSION "")
set(X264_POINTVER "0.${X264_BUILD}.x")
find_program(X264_SH_EXEC sh)
if(X264_SH_EXEC)
    execute_process(
        COMMAND ${X264_SH_EXEC} ${PROJECT_SOURCE_DIR}/version.sh
        WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}
        OUTPUT_VARIABLE X264_VERSION_OUTPUT
        RESULT_VARIABLE X264_VERSION_RESULT
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    if(X264_VERSION_RESULT EQUAL 0 AND X264_VERSION_OUTPUT)
        string(REPLACE "\r\n" "\n" X264_VERSION_OUTPUT "${X264_VERSION_OUTPUT}")
        string(REPLACE "\n" ";" X264_VERSION_LINES "${X264_VERSION_OUTPUT}")
        foreach(_line ${X264_VERSION_LINES})
            string(STRIP "${_line}" _line)
            if(_line MATCHES "#define X264_REV ([0-9]+)")
                set(X264_REV "${CMAKE_MATCH_1}")
            elseif(_line MATCHES "#define X264_REV_DIFF ([0-9]+)")
                set(X264_REV_DIFF "${CMAKE_MATCH_1}")
            elseif(_line MATCHES "#define X264_VERSION \"(.*)\"")
                set(X264_VERSION "${CMAKE_MATCH_1}")
            elseif(_line MATCHES "#define X264_POINTVER \"(.*)\"")
                set(X264_POINTVER "${CMAKE_MATCH_1}")
            endif()
        endforeach()
    else()
        message(STATUS "version.sh execution failed; using fallback version metadata")
    endif()
else()
    message(STATUS "Posix shell not found; using fallback version metadata")
endif()
